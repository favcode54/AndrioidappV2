apply plugin: 'com.android.application'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "org.favcode54"
        minSdkVersion 17
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        /**
         * Room schema location
         */
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    /**
     * Exclude rxjava.properties because it's not used and more
     * than one library is importing it. (Room and Retrofit)
     */
    packagingOptions{
        exclude 'META-INF/rxjava.properties'
    }

    /**
     * Changed compile options to Java 8 to support lambda expressions
     */
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding{
        enabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "BASE_URL", "\"https://portal.favcode54.org/API/\"")
        }
        debug {
            minifyEnabled false
            setVersionNameSuffix("-debug")
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "BASE_URL", "\"https://portal.favcode54.org/API/\"")
        }
    }
}

dependencies {
    def support_version = "27.1.1"
    def dagger_2_version = "2.14.1"
    def lifecycle_version = "0.6.1"
    def room_version = "1.1.1"

    implementation fileTree(dir: 'libs', include: ['*.jar'])

    //Testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //Support libraries
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation "com.android.support:recyclerview-v7:$support_version"
    implementation "com.android.support:cardview-v7:$support_version"

    //Dependency injection
    implementation "com.google.dagger:dagger:$dagger_2_version"
    annotationProcessor "com.google.dagger:dagger-compiler:$dagger_2_version"

    //Network
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:okhttp-urlconnection:3.4.2'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    implementation 'com.jakewharton.timber:timber:4.6.0'

    //Lifecycle
    implementation "com.trello:rxlifecycle:$lifecycle_version"
    implementation "com.trello:rxlifecycle-components:$lifecycle_version"

    //Room
    implementation "android.arch.persistence.room:runtime:$room_version"
    implementation "android.arch.persistence.room:rxjava2:$room_version"
    annotationProcessor "android.arch.persistence.room:compiler:$room_version"
}
